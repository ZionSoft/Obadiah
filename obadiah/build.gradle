buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility rootProject.ext.sourceJavaVersion
        targetCompatibility rootProject.ext.targetJavaVersion
    }

    defaultConfig {
        versionName rootProject.ext.versionName
        versionCode rootProject.ext.versionCode

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        applicationId 'net.zionsoft.obadiah'

        resConfigs "en", "fi", "zh", "zh-rCN"
    }

    signingConfigs {
        release {
            if (project.hasProperty('KEYSTORE_FILE')) {
                storeFile file(KEYSTORE_FILE)
                storePassword KEYSTORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard.txt'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibVersion"

    compile "com.google.firebase:firebase-auth:$rootProject.ext.firebaseVersion"
    compile "com.google.firebase:firebase-core:$rootProject.ext.firebaseVersion"
    compile "com.google.firebase:firebase-ads:$rootProject.ext.firebaseVersion"
    compile "com.google.firebase:firebase-appindexing:$rootProject.ext.firebaseVersion"
    compile "com.google.firebase:firebase-crash:$rootProject.ext.firebaseVersion"
    compile "com.google.firebase:firebase-database:$rootProject.ext.firebaseVersion"
    compile "com.google.firebase:firebase-invites:$rootProject.ext.firebaseVersion"
    compile "com.google.firebase:firebase-messaging:$rootProject.ext.firebaseVersion"

    compile "com.google.android.gms:play-services-auth:$rootProject.ext.playServicesVersion"

    compile "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    provided "javax.annotation:javax.annotation-api:$rootProject.ext.javaxAnnotationApiVersion"

    compile "io.reactivex:rxandroid:$rootProject.ext.rxAndroidVersion"
    compile "io.reactivex:rxjava:$rootProject.ext.rxJavaVersion"
    compile "com.artemzin.rxjava:proguard-rules:$rootProject.ext.rxJavaProguardRulesVersion"

    compile "com.jakewharton:butterknife:$rootProject.ext.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterKnifeVersion"

    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    compile "com.squareup.retrofit2:converter-moshi:$rootProject.ext.retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.ext.retrofitVersion"

    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttpVersion"
    compile "com.squareup.moshi:moshi:$rootProject.ext.moshiVersion"
    compile "com.squareup.okio:okio:$rootProject.ext.okioVersion"

    compile "com.github.xizzhu.auto-droid:auto-droid-annotations:$rootProject.ext.autoDroidVersion"
    annotationProcessor "com.github.xizzhu.auto-droid:auto-droid-processors:$rootProject.ext.autoDroidVersion"
    provided "com.google.auto.value:auto-value:$rootProject.ext.autoValueVersion"

    debugCompile "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.stethoVersion"

    testCompile "junit:junit:$rootProject.ext.junitVersion"
}

apply plugin: 'com.google.gms.google-services'
